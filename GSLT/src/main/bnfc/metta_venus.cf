{-
    MeTTa IL: Language Syntax Definition
-}

-- A Space is a GSLT and a FactBase

SpaceDeclProg . Prog ::= SpaceDecl ;
ThDeclProg . Prog ::= ThDecl ;

SpaceDeclNoDepsImpl . SpaceDecl ::= "Space" Name "(" [VariableDecl] ")" "=" "{" [Fact] [FactComprehension] "}" ;
SpaceDeclDepsImpl . SpaceDecl ::= "Space" Name "(" [VariableDecl] ";" [VariableDecl] ")" "=" "{" [Fact] [FactComprehension] "}" ;

_. SpaceInst  ::= SpaceInst1 ;
_. SpaceInst1 ::= "{" SpaceInst "}" ;

SpaceInstEmpty. SpaceInst1 ::= "Empty" ;
SpaceInstImpl . SpaceInst1 ::= Name "(" [ThInst] ";" [SpaceInst] ")" ;
SpaceInstRec . SpaceInst1 ::= "let" Ident "=" SpaceInst1 "in" SpaceInst1 ;
SpaceInstRef . SpaceInst1 ::= Ident ;
SpaceInstPar . SpaceInst ::= SpaceInst "|" SpaceInst1 ;
separator SpaceInst "," ; 

-- Names
NameWildcard. Name ::= "_" ;
NameVar.      Name ::= Ident ;
--NameQuote.    Name ::= "@" Proc12 ;
separator Name "," ;

NameRemainderVar.   NameRemainder ::= "..." "@" Ident ;
NameRemainderEmpty. NameRemainder ::= "" ;

VarDecl . VariableDecl ::= Ident ":" Name ;
separator VariableDecl "," ;

FactImpl . Fact ::= Item ;
separator Fact "," ;

FactComprehensionImpl . FactComprehension ::= "for" "(" [ReceiptSymm] ")" "{" SpaceInst "}" ;
separator FactComprehension "," ;

-- ReceiptSymm
ReceiptSymmLinear.    ReceiptSymm ::= ReceiptSymmLinearImpl ;
ReceiptSymmRepeated.  ReceiptSymm ::= ReceiptSymmRepeatedImpl ;
ReceiptSymmPeek.      ReceiptSymm ::= ReceiptSymmPeekImpl ;
separator nonempty ReceiptSymm ";" ;

-- Linear ReceiptSymms
LinearSymmSimple. ReceiptSymmLinearImpl ::= [LinearBindSymm] ;
-- Implementing this will be tricky.
-- for (x <- a; y <- b if *x)
-- LinearCond.   Linear ::= [LinearBindSymm] "if" Proc ;

-- Single Linear BindSymm
LinearBindSymmImpl. LinearBindSymm ::= [Name] NameRemainder "<->" NameSymmSource ;
separator nonempty LinearBindSymm "&" ;

SimpleSymmSource.         NameSymmSource ::= Name ;
Self.                     NameSymmSource ::= "self" ;
ReceiveSendSymmSource.    NameSymmSource ::= Name "?!" ;
SendReceiveSymmSource.    NameSymmSource ::= Name "!?" "(" [Fact] ")" ;

-- Repeated ReceiptSymms
RepeatedSymmSimple. ReceiptSymmRepeatedImpl ::= [RepeatedBindSymm] ;
-- Single Repeated BindSymm
RepeatedBindSymmImpl. RepeatedBindSymm ::= [Name] NameRemainder "<=>" Name ;
separator nonempty RepeatedBindSymm "&" ;

-- Peek ReceiptSymms
PeekSymmSimple. ReceiptSymmPeekImpl ::= [PeekBindSymm] ;
-- Single Peek
PeekBindSymmImpl. PeekBindSymm ::= [Name] NameRemainder "<<->>" Name ;
separator nonempty PeekBindSymm "&" ;

-- A GSLT is a Grammar, a set of equations, and a list of rewrite rules

GSLTDecl . ThDecl ::= "Theory" Name "(" [VariableDecl] ")" "=" "{" FreeTheory ";" Equations ";" Rewrites "}" ;

GSLTInst . ThInst ::= Name "(" [ThInst] ")" ;
GSLTRec . ThInst ::= "let" Ident "=" ThInst "in" ThInst ;
GSLTRef . ThInst ::= Ident ;
separator ThInst "," ;

Generators . FreeTheory ::= "Terms" "{" Grammar "}" ;
Relations . Equations ::= "Equations" "{" [Equation] "}" ;
Transitions . Rewrites ::= "Rewrites" "{" [RewriteDecl] "}" ;

-- A Grammar is a sequence of definitions

MkGrammar . Grammar ::= [Def] ;

[]  . [Def] ::= ;
(:) . [Def] ::= Def ";" [Def] ;

[]  . [Item] ::= ;
(:) . [Item] ::= Item [Item] ;

--The rules of the grammar
Rule . Def ::= Label "." Cat "::=" [Item] ;

-- Items
Terminal     . Item ::= String ;
NTerminal    . Item ::= Cat ;
-- IntList specifies where in the term the variable is bound.
--    Lam. T ::= "Î»" (Bind [1]) "." T                             -- bound in body
--    For. P ::= "for" "(" (Bind [2]) "<-" Ident ")" "{" P "}"    -- not bound in channel name
-- Can bind the variable in subterms appearing before the binder.
--    Foo. P ::= "foo" P "bar" (Bind [0, 3]) "baz" P ":" P        -- not bound in slot 2
-- Error if the list is empty, contains its own slot, or contains
--    a number greater than or equal to the number of slots.
-- Error if Ident is the RHS of any rule for categories in named slots.
-- Effectively adds Ident as RHS of new rule.
-- Parser generated from GSLT accepts Ident in this position.
BindTerminal . Item ::= "(" "Bind" IntList ")" ;

-- Categories
ListCat  . Cat ::= "[" Cat "]" ;
IdCat    . Cat ::= Ident ;

-- labels with or without profiles
LabNoP   . Label ::= LabelId ;
LabP     . Label ::= LabelId [ProfItem] ;
LabPF    . Label ::= LabelId LabelId [ProfItem] ;
LabF     . Label ::= LabelId LabelId ;

-- functional labels
Id       . LabelId ::= Ident ;
Wild     . LabelId ::= "_" ;
ListE    . LabelId ::= "[" "]" ;
ListCons . LabelId ::= "(" ":" ")" ;
ListOne  . LabelId ::= "(" ":" "[" "]" ")" ;

-- profiles (= permutation and binding patterns)
ProfIt   . ProfItem ::= "(" "[" [IntList] "]" "," "[" [Integer] "]" ")" ;

Ints     . IntList ::= "[" [Integer] "]" ;

separator Integer "," ;
separator IntList "," ;
terminator nonempty ProfItem "" ;

-- Pragmas
Comment  .  Def ::= "comment" String ;
Comments .  Def ::= "comment" String String ;
Internal .  Def ::= "internal" Label "." Cat "::=" [Item] ;
Token.      Def ::= "token" Ident Reg ;
PosToken.   Def ::= "position" "token" Ident Reg ;
Entryp.     Def ::= "entrypoints" [Ident] ;
Separator.  Def ::= "separator" MinimumSize Cat String ;
Terminator. Def ::= "terminator" MinimumSize Cat String ;
Coercions.  Def ::= "coercions" Ident Integer ;
Rules.      Def ::= "rules" Ident "::=" [RHS] ;

Layout.     Def ::= "layout" [String] ;
LayoutStop. Def ::= "layout" "stop" [String] ;
LayoutTop.  Def ::= "layout" "toplevel" ;

separator nonempty String "," ;

separator nonempty RHS "|" ;
MkRHS. RHS ::= [Item] ;

-- List size condition
MNonempty.  MinimumSize ::= "nonempty" ;
MEmpty.     MinimumSize ::=  ;

-- regular expressions
RSeq.   Reg2 ::= Reg2 Reg3 ;
RAlt.   Reg1 ::= Reg1 "|" Reg2 ;
RMinus. Reg1 ::= Reg2 "-" Reg2 ;

RStar.  Reg3 ::= Reg3 "*" ;
RPlus.  Reg3 ::= Reg3 "+" ;
ROpt.   Reg3 ::= Reg3 "?" ;

REps.   Reg3 ::= "eps" ;

RChar.  Reg3 ::= Char ;           -- single character
RAlts.  Reg3 ::= "[" String "]" ; -- list of alternative characters
RSeqs.  Reg3 ::= "{" String "}" ; -- character sequence

RDigit.  Reg3 ::= "digit" ;
RLetter. Reg3 ::= "letter" ;
RUpper.  Reg3 ::= "upper" ;
RLower.  Reg3 ::= "lower" ;
RAny.    Reg3 ::= "char" ;

_. Reg  ::= Reg1 ;
_. Reg1 ::= Reg2 ;
_. Reg2 ::= Reg3 ;
_. Reg3 ::= "(" Reg ")" ;

-- list of categories in the entrypoint pragma
(:[]).  [Ident] ::= Ident ;
(:).    [Ident] ::= Ident "," [Ident] ;

-- Equations

EquationImpl . Equation ::= AST "==" AST ;
separator Equation ";" ;

-- Rewrites

RDecl . RewriteDecl ::= Ident ":" Rewrite ;
separator RewriteDecl ";" ;

RewriteBase    . Rewrite ::= AST "~>" AST ;
RewriteContext . Rewrite ::= "let" Hypothesis "in" Rewrite ;

ASTSubst . AST ::= "(" "Subst" AST AST Ident ")";
ASTVar   . AST ::= Ident ;
ASTSExp  . AST ::= "(" Ident "." Ident [AST] ")" ;

[]  . [AST] ::= ;
(:) . [AST] ::= AST [AST] ;

Hyp . Hypothesis ::= Ident "~>" Ident ;

--token Var (((letter | '\'') (letter | digit | '_' | '\'')*)|(('_') (letter | digit | '_' | '\'')+)) ;

--token CapIdent (((upper | '\'') (letter | digit | '_' | '\'')*)|(('_') (letter | digit | '_' | '\'')+)) ;

--token Ident (((upper | '\'') (letter | digit | '_' | '\'')*)|(('_') (letter | digit | '_' | '\'')+)) ;

-- comments in BNF source
comment "--" ;
comment "{-" "-}" ;